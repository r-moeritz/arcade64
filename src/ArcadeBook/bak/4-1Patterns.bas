5 DIM A$(7),X$(1),A(7)
10 VM=0:CH=53248
15 X$(0)="P":X$(1)="{reverse on} {reverse off}"
20 C$="{cm t}{cm t}{cm t}{cm t}{cm t}{cm t}{cm t}{cm t}"
97 REM
98 REM MAIN LOOP
99 REM
100 GET A$:IF A$="" THEN 100
110 A=ASC(A$):Q=A:GOSUB 200:GOSUB 300
120 PRINT B$:PRINT A$:FOR I=0 TO 7:PRINT A$(I);A(I):NEXT:PRINT C$
130 GOTO 100
197 REM
198 REM convert ascii to screen code
200 IF A>63 AND A<96 THEN A=A-64:RETURN
210 IF A>95 AND A<128 THEN A=A-32:RETURN
220 IF A>159 AND A<192 THEN A=A-64:RETURN
230 IF A>127 AND A<160 THEN A=A+64:RETURN
240 IF A<32 THEN A=A+128:RETURN
250 IF A>191 AND A<255 THEN A=A-128:RETURN
260 IF A=255 THEN A=94:RETURN
297 REM
298 REM MAKE STRINGS TO PRINT PATTERN
299 REM
300 B$="screen code "+STR$(A)
310 IF A<128 THEN T=A:GOSUB 400:A$=A$+"  chr$("+STR$(T)+")"
320 IF A>127 THEN T=A-128:GOSUB 400:A$="reverse-"+ CHR$(T)+"  chr$("+STR$(Q)+")"
330 X=8*A:GOSUB 800:GOSUB 700
390 RETURN
397 REM
398 REM Convert screen code to ascii
399 rem
400 IF T<32 THEN T=T+64:RETURN
410 IF T>63 AND T<96 THEN T=T+32:RETURN
420 IF T>95 THEN T=T+64:RETURN
430 return
697 rem
698 rem take appart patter bytes
699 rem
700 FOR I=0 TO 7:A$(I)="":NEXT
710 FOR I=0 TO 7:W=A(I):FOR J=0 TO 7
720 W=2*W:IF W>255 THEN W=W-256:A$(I)=A$(I)+X$(1):goto 740
730 A$(I)=A$(I)+X$(0)
740 NEXT J:NEXT I:RETURN
797 REM
798 rem get pattern bytes
799 rem
800 POKE 56334,PEEK(56334)AND 254:POKE 1,PEEK(1)AND 251
810 PT=CH+X:FOR I=0 TO 7:A(I)=PEEK(PT+I):NEXT
820 POKE 1,PEEK(1) OR 4:POKE 56334,PEEK(56334)OR 1
830 return
